// This gradle build depends on Mendix internal resources, the lastest commit
// which did not is 6e1527a5e31347c6914bc34deab31ef4ab46953b. You may restore
// the gradle scripts to that commit if you which to use them at your own risk.
// We don't guarantee that those will keep working in the future.

plugins {
    id 'java'
    id 'com.mendix.gradle.publish-module-plugin' version '1.14'
    id 'net.researchgate.release' version '2.8.1'
}

sourceCompatibility = '11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext {
    mendixPublicApiVersion = '9.18.0'
}

mxMarketplace {
    appNumber = 170
    appName = 'Community Commons'
    moduleName = 'CommunityCommons'
    moduleLicense = 'Apache V2'
    appDirectory = "src/CommunityCommons"
    versionPathPrefix = "_Version " // the path prefix within the module to the version folder
    createMigrationFile = true
}

def userLibDir = "$projectDir/src/CommunityCommons/userlib"

repositories {
    maven {
        url 'https://nexus.rnd.mendix.com/repository/repo1-proxy/'
    }
    maven {
        url 'https://nexus.rnd.mendix.com/repository/maven-hosted/'
    }
}

configurations {
    implementation {
        canBeResolved = true
    }
}

dependencies {
    testImplementation(
            [group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"],
            [group: 'junit', name: 'junit', version: '4.13.1'],
            [group: 'org.apache.httpcomponents.core5', name: 'httpcore5', version: '5.0.3'],
            [group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.0.3'],
            [group: 'org.hamcrest', name: 'hamcrest', version: '2.2']
    )

    compileOnly([group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"])

    implementation(
            [group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'],
            [group: 'com.googlecode.owasp-java-html-sanitizer', name: 'owasp-java-html-sanitizer', version: '20211018.2'],
            [group: 'commons-io', name: 'commons-io', version: '2.17.0'],
            [group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.30'],
            [group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'],
            [group: 'org.apache.commons', name: 'commons-text', version: '1.10.0']
    )
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

sourceSets {
    main {
        java {
            srcDir 'src/CommunityCommons/javasource'
            exclude 'unittesting/**'
            exclude 'system/UserActionsRegistrar.java'
        }
    }
    test {
        java {
            srcDirs = ["src/test", "src/CommunityCommons/javasource"]
        }
    }
}

test {
    exclude 'unittesting/*'
}

task copyAllToUserlib(type: Copy) {
    from configurations.testCompileClasspath
    into userLibDir
    eachFile { fileCopyDetails -> new File(destinationDir, "${fileCopyDetails.name}.${project.name}.RequiredLib").write '' }
}

tasks.named('mxBuild') {
    dependsOn copyAllToUserlib
}

tasks.named('compileJava') {
    dependsOn mxBuild
}

clean {
    delete "$userLibDir"
}

release {
    tagTemplate = '$name-$version'
}

task afterReleaseBuildTask {
    dependsOn 'clean'
    dependsOn 'publishModuleToMarketplace'

    tasks.findByName('publishModuleToMarketplace').mustRunAfter 'clean'
}

afterReleaseBuild.dependsOn afterReleaseBuildTask
